plugins {
    id 'org.springframework.boot' version "$springbootVersion"
}
apply plugin: 'io.spring.dependency-management'
apply plugin: 'distribution' //打包tar包用到的插件
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compile project(":browser-service")
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    compile "com.google.guava:guava:$guavaVersion"

    compile group: 'com.lmax', name: 'disruptor', version: '3.4.2'
    compile group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.4'
    testCompile group: 'org.hsqldb', name: 'hsqldb', version: '2.5.0'
}

//判断 build命令是否指定属性 profile。
//例如 gradle buildTar -xtest -Pprofile=online

ext {
    if (project.hasProperty('profile')) {
        profile = project['profile']
    } else {
        profile = "all"
    }
    println "profile:" + profile
}

// task 用来新建一些目录，目录位于build/package下
task createDirs() {
	file('build/lib').mkdirs()
    file('build/package/browser-agent').mkdirs()
    file('build/package/lib').mkdirs()
}
//task 用来复制build出来的主jar包
task copyLibs(type: Copy) {
    from('build/libs')
    from('jasypt.properties')
    from('status.hook')
    into('build/package/browser-agent')
}
//task 用来复制配置文件
task copyConf(type: Copy) {
	if(profile != 'all') {
	    println 'copy conf from src/main/resources/application-'+profile+'.yml'
	    from('src/main/resources/application-'+profile+'.yml')
	    into('build/package/browser-agent')
    } else {
    	println 'copy conf from src/main/resources all'
	    from('src/main/resources/application-hrplat100.yml')
	    into('build/package/browser-agent')
	    from('src/main/resources/application-hrpatp201018v01500.yml')
	    into('build/package/browser-agent')
	    from('src/main/resources/application-prod.yml')
	    into('build/package/browser-agent')
	    from('jasypt.properties')
	    into('build/package/browser-agent')
    }
}
//task 用来复制bin下的脚本。这里的fileMode并没有生效，原因不详
task copyBin(type: Copy) {
    from('src/main/resources/bin')
    into('build/package/browser-agent')
    fileMode 0744
}
// task 用来复制启动所依赖的jar包
task copyDep(type: Copy) {
    from configurations.runtime
    into 'build/lib'
}
// task 用来复制指定加密包
task copyJasypt(type: Copy) {
    from 'build/lib/jasypt-1.9.2.jar'
    into 'build/package/lib'
}

// task 用来删除lib
task deleteLib(type: Delete) {
    delete 'build/lib'
}
//把上述的task串联起来
task prepareFile(dependsOn: [
        'createDirs',
        'copyLibs',
        'copyConf',
        'copyBin',
        'copyDep',
        'copyJasypt',
        'deleteLib'
]){}
//指定打包的tar包的名字，以及文件来源目录
distributions {
    monitor {
        baseName = 'browser-agent'
        contents {
            from { 'build/package' }
        }
    }

}

//distribution 插件的特性，以DistTar结尾
monitorDistTar.dependsOn  'prepareFile'
monitorDistTar.compression = Compression.GZIP
monitorDistTar.extension = 'tar.gz'

//定义一个task，先build 然后再打包tar包
task buildTar(dependsOn: [
        'build',
        monitorDistTar
]){}


sonarqube {
    properties {
        property "sonar.exclusions", "src/main/java/**/dao/param**,**/*Mapper.java," +
                "**/*Application.java,**/NodeUpdateTask.java,**/NetworkStatUpdateTask.java," +
                "**/ProposalInfoTask.java,**/AddressUpdateTask.java,**/EpochMessage.java," +
                "**/NetworkStatCache.java,**/ShutdownCallback.java,**/AccountService.java," +
                "**/OnSettleConverter.java,**/PersistenceEvent.java,**/AddressTypeEnum.java," +
                "**/ConfigChange.java,**/TxAnalyseResult.java,**/DelegateExitResult.java," +
                "**/CalculateUtils.java,**/PersistenceEventHandler.java,**/DelegateRewardClaimConverter.java," +
                "**/EsDelegateRewardService.java,**/ERC20Client.java,**/TransferEventConverter.java,"+
                "**/StakingDelegateBalanceAdjustmentServiceTest2.java"

    }
}

test {
    //   include '**/Test*.class'
    // 忽略指定的测试用例
    exclude '/**/ProposalContractTest.class'
    exclude '/**/RestrictingPlanContractTest.class'
    exclude '/**/SlashContractTest.class'
    exclude '/**/TransactionSender.class'
    exclude '/**/CustomNodeMapperTest.class'
    exclude '/**/StakingContractTest.class'
    exclude '/**/WASMUploadTest.class'
    exclude '/**/WASMDestructTest.class'
    exclude '/**/SuicideAndSelfdestructTest.class'
    exclude '/**/HumanStandardTokenTest.class'
    exclude '/**/proxyppos/*Test.class'
    exclude '/**/proxyppos/**/*Test.class'
    exclude '/**/StakingDelegateBalanceAdjustmentServiceTest2.class'
}
